// 2 KTM-5M3
switch (FEAT_ROADVEHS, SELF, sw_articulated_2ktm5m3, extra_callback_info1) {
	1..2: return item_tram_2ktm5m3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_ROADVEHS, SELF, sw_cargo_subtype_text_2ktm5m3, cargo_subtype) {
	0: return string(STR_CARGO_KTM5M3_1);
	1: return string(STR_CARGO_KTM5M3_2);
	return CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_spriteset_rear_2ktm5m3, cargo_subtype) {
	0: return spriteset_invisible;
	1: return spritegroup_tram_ktm5m3;
}
switch (FEAT_ROADVEHS, SELF, sw_spriteset_2ktm5m3, position_in_consist) {
	0: return spritegroup_tram_ktm5m3;
	1: return spriteset_invisible;
	2: sw_spriteset_rear_2ktm5m3;
}
switch (FEAT_ROADVEHS, SELF, sw_length_2ktm5m3, cargo_subtype) {
	0: return (position_in_consist == 0) ? 8 : 1;
	1: return (position_in_consist != 1) ? 8 : 5;
}
item(FEAT_ROADVEHS, item_tram_2ktm5m3) {
    property {
        name:                           string(STR_NAME_2KTM5M3);
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1972,12,01);
        model_life:                     20;
        vehicle_life:                   20;
        reliability_decay:              20;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        loading_speed:                  8;
        cost_factor:                    116;
        running_cost_factor:            40*2;
		visual_effect:                  visual_effect(VISUAL_EFFECT_ELECTRIC, -4);
		sound_effect:                   sound("bell.wav");
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;
        speed:                          75 km/h;
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        running_cost_base:              RUNNING_COST_ROADVEH;
        power:                          480 hp;
        weight:                         38 ton;
        cargo_capacity:                 224;
    }
    graphics {
		additional_text:                return string(STR_DESC_2KTM5M3);
		articulated_part:               sw_articulated_2ktm5m3;
		cargo_subtype_text:             sw_cargo_subtype_text_2ktm5m3;
		length:                         sw_length_2ktm5m3;
        default:                        sw_spriteset_2ktm5m3;
    }
}
